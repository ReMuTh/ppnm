set terminal svg size 750,525 dynamic
set output 'jacobi_timing.svg' 
set xlabel 'Symmetric matrix size [N]' 
set ylabel 'Time [ms]' 
set key top left 
set title '{/:Bold Time to do Jacobi eigenvalues}'
f(x) = a*x*x*x
f2(x) = b*x*x*x
fit f(x) "jacobi_timing.txt" using 1:2 via a
fit f2(x) "jacobi_timing_opti.txt" using 1:2 via b
tia = sprintf("Fit to f(n) = a * n^3  a = %.2e ms", a)
tib = sprintf("Fit to f(n) = b * n^3  b = %.2e ms", b)
plot [0:230] \
		'jacobi_timing.txt' using 1:2 with points title 'Sweeping all elements' pt 1 lt rgb "red",\
		'jacobi_timing_opti.txt' using 1:2 with points title 'Sweeping only upper triangle' pt 1 lt rgb "green",\
		f(x) t tia dt 2 lt rgb "red",\
		f2(x) t tib dt 2 lt rgb "green"

